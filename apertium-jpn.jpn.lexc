! Morphological Transducer for Japanese

Multichar_Symbols

! Part of speech categories
%<n%>   ! Noun
%<pn%>  ! Proper noun
%<v%>	! Verb
%<adj%> ! Adjective
%<adv%> ! Adverb
%<cnjcoo%> ! Coordinating conjunction
%<ij%>  ! Interjection
%<prn%> ! Pronoun
%<cop%>	! Copula
%<num%> ! Numeral
%<det%> ! Determiner

%<dem%> ! Demonstrative

%<top%> ! toponym (should probably be changed?)

%<attr%> ! for na adjectives

! Particles
%<tpc%> ! Topic
%<nom%>  ! Nominative
%<gen%>  ! Genitive
%<acc%>  ! Accusative
%<dat%>  ! Dative
%<quot%> ! Quotative
%<all%> ! Allative
%<abl%>  ! Ablative
%<ins%>  ! Instrumental
%<com%>  ! Comitative
%<term%> ! Terminative
%<mod%>  ! For final particles
%<abs%>  ! For things that don't have postpositions

! -alisers
%<nz%>  ! Nominaliser
%<az%>  ! Adverbaliser
%<vz%>  ! Verbaliser

! Pronoun-related
%<p1%>   ! First person
%<p2%>   ! Second person
%<p3%>   ! Third person
%<pers%> ! Personal

! verb suffixes
%<vol%>  ! Volitional
%<neg%>  ! Negative
%<past%> ! Past
%<imp%>  ! Imperative
%<npst%> ! Non-past
!conditional?
%<caus%>  ! Causative
%<pot%>   ! Potential
%<pass%>  ! Passive
%<cvb_te%> ! te form, because I don't know what it is
%<prog%> ! progressive

! Number morphology
%<pl%>  ! Plural
%<sg%>  ! Singular

! Formality
%<pol%>	! Polite
%<ifml%>! Informal

!Punctuation
%<sent%>
%<cm%>
%<lquot%>
%<rquot%>
%<lpar%>
%<rpar%>

! Other symbols
%>      ! Morpheme boundary
%{っ%} ! for use in ta and te form
%{a%} ! for use in neg form
%{た%} ! for use in ta form
%{て%} ! for te form
%{i%} ! for adverbial form

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                       M O R P H O T A C T I C S                         !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

NounRoot ;

ProperNouns ;

VerbRoot ;

AdjRoot ;

Pronouns ;

Conjunctions ;

Determiners ;

!Interjections ;

Adverbs ;

Digits ;

Punctuation ;


LEXICON NounTag
%<n%>: NounPl ;

LEXICON NPTop
%<pn%>%<top%>: NounInfl ;
%<pn%>%<top%>%+人:%>人 NounInfl ; ! person (effectively, citizen) じん
%<pn%>%<top%>%+軍:%>軍 NounInfl ; ! military ぐん
%<pn%>%<top%>%+国内:%>国内 NounInfl ; ! domestic こくない
%<pn%>%<top%>%+国外:%>国外 NounInfl ; ! foreign こくがい

LEXICON NounPl
%<sg%>: NounInfl ;
%<pl%>:%>たち NounInfl ;
%<pl%>:%>達 NounInfl ;

LEXICON iAdj
%<adj%>%<npst%>:%>い #;
%<adj%>%+です%<cop%>%<pol%>%<npst%>:%>いです FinPart ;
%<adj%>%<ifml%>%<past%>:%>かった FinPart ;
%<adj%>%<ifml%>%<npst%>%<neg%>:%>くない FinPart ;
%<adj%>%<ifml%>%<past%>%<neg%>:%>くなかった FinPart ;
%<adj%>%<az%>:%>く # ;


LEXICON naAdjTag
%<adj%>: NounInfl ;
%<adj%>%<attr%>:%>な # ;

LEXICON NounInfl
%<cop%>%<pol%>%<npst%>:%>です FinPart ;
%<cop%>%<ifml%>%<npst%>:%>だ FinPart ;
%<pol%>%<cop%>%<past%>:%>でした FinPart ;
%<ifml%>%<cop%>%<past%>:%>だった FinPart ;
PostPos ;

LEXICON PostPos
%<tpc%>:%>は # ;
%<nom%>:%>が # ;
%<gen%>:%>の # ;
%<acc%>:%>を # ;
%<dat%>:%>に # ;
%<quot%>:%>と # ;
%<abl%>:%>から # ;
%<ins%>:%>で # ;
%<all%>:%>へ # ;
%<term%>:%>まで # ;
! mo
! ya
%<abs%>: # ;

LEXICON FinPart
%+わ%<mod%>:%>わ # ; !
%+よ%<mod%>:%>よ # ; ! Informing the hearer
%+ね%<mod%>:%>ね # ; ! Emphasis
%+か%<mod%>:%>か # ; ! Question

LEXICON UVerbInflCP
%<v%>: UVerbInfl ;
%<v%>%<caus%>:%>%{a%}せ RuVerbInfl ;
%<v%>%<pass%>:%>%{a%}れ RuVerbInfl ;
%<v%>%<caus%>%<pass%>:%>%{a%}せられ RuVerbInfl ;

LEXICON RuVerbInflCP
%<v%>: RuVerbInfl ;
%<v%>%<caus%>:%>させ RuVerbInfl ;
%<v%>%<pass%>:%>られ RuVerbInfl ;
%<v%>%<caus%>%<pass%>:%>させられ RuVerbInfl ;

LEXICON UVerbInfl
%<ifml%>%<npst%>: PostShortForm ;
%<ifml%>%<past%>:%>%{っ%}%{た%} PostShortForm ;
%<ifml%>%<npst%>%<neg%>:%>%{a%}ない PostShortForm ;
%<ifml%>%<past%>%<neg%>:%>%{a%}なかった PostShortForm ;
%<pol%>%<npst%>:%>%{i%}ます # ;
%<pol%>%<past%>:%>%{i%}ました # ;
%<pol%>%<npst%>%<neg%>:%>%{i%}ません # ;
%<pol%>%<past%>%<neg%>:%>%{i%}ませんでした # ;
%<cvb_te%>:%>%{っ%}%{て%} # ; ! needs to go to a post-te lexicon

LEXICON RuVerbInfl
%<ifml%>%<npst%>:%>る PostShortForm ;
%<ifml%>%<past%>:%>た PostShortForm ;
%<ifml%>%<npst%>%<neg%>:%>ない PostShortForm ;
%<ifml%>%<past%>%<neg%>:%>なかった PostShortForm ;
%<pol%>%<npst%>:%>ます # ;
%<pol%>%<past%>:%>ました # ;
%<pol%>%<npst%>%<neg%>:%>ません # ;
%<pol%>%<past%>%<neg%>:%>ませんでした # ;
%<cvb_te%>:%>て PostTeForm ;

LEXICON PostTeForm
# ;
%<prog%>:%>いる # ; ! this needs to get inflected

LEXICON PostShortForm
# ;
%<nz%>:%>こと NounInfl ;

LEXICON LOOP
%,:%, DIGITLEX ;
%.:%. DIGITLEX ;
%,:%, POST-DIGITS ;
%.:%. POST-DIGITS ;
      DIGITLEX ;
      POST-DIGITS ;

LEXICON DIGITLEX
%0:%0 LOOP ;
1:1   LOOP ;
2:2   LOOP ;
3:3   LOOP ;
4:4   LOOP ;
5:5   LOOP ;
6:6   LOOP ;
7:7   LOOP ;
8:8   LOOP ;
9:9   LOOP ;
一:一 LOOP ;# ; ! one
二:二 LOOP ;# ; ! two
三:三 LOOP ;# ; ! three
四:四 LOOP ;# ; ! four
五:五 LOOP ;# ; ! five
六:六 LOOP ;# ; ! six
七:七 LOOP ;# ; ! seven
八:八 LOOP ;# ; ! eight
九:九 LOOP ;# ; ! nine
十:十 LOOP ;# ; ! ten
いち:いち LOOP ; # ; ! one
に:に LOOP ;# ; ! two
さん:さん LOOP ;# ; ! three
よん:よん LOOP ;# ; ! four
し：し LOOP ; # ; ! four
ご:ご LOOP ; # ; ! five
ろく:ろく LOOP ;# ; ! six
なな:なな LOOP ;# ; ! seven
しち:しち LOOP ;# ; ! seven
はち:はち LOOP ;# ; ! eight
きゅう:きゅう LOOP ;# ; ! nine
じゅう:じゅう LOOP ;# ; ! ten

LEXICON POST-DIGITS
%<num%>: PostPos ;
年%<num%>:年 PostPos ; ! year ねん
月%<num%>:月 PostPos ; ! month がつ
日%<num%>:日 PostPos ; ! day にち
時%<num%>:時 PostPos ; ! hour じ
分%<num%>:分 PostPos ; ! minute ぶん
秒%<num%>:秒 PostPos ; ! second びょう



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                          L E X I C O N                                  !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON NounRoot
家:家 NounTag ; ! house うち
広島:広島 NounTag ; ! Hiroshima
広島市:広島市 NounTag ; ! Hiroshima city
原爆:原爆 NounTag ; ! atomic げんばく
原爆ドーム:原爆ドーム NounTag ; ! atom bomb dome
原爆爆弾:原爆爆弾 NounTag ; ! atomic bomb げんばくばくだん
平和:平和 NounTag ; ! peace へいわ
公園:公園 NounTag ; ! park こうえん
広島市中央公園:広島市中央公園 NounTag ; ! Hiroshima central park ひろしまちゅうおこうえん
世界:世界 NounTag ; ! world せかい
設計:設計 NounTag ; ! せっけい design/layout
爆心地:爆心地 NounTag ; ! ばくしんち hypocentre
資料館:資料館 NounTag ; ! しりょうかん museum/archive
構想:構想 NounTag ; ! こうそう plan
バラック:バラック NounTag ; ! barracks
区域:区域 NounTag ; ! くいき limits
繁華街:繁華街 NounTag ; ! はんかがい downtown
地区:地区 NounTag ; ! ちく district
グレープフルーツ:グレープフルーツ NounTag ; ! grapefruit
ジュース:ジュース NounTag ; ! juice
マーマレード:マーマレード NounTag ; ! marmalade
ジャム:ジャム NounTag ; ! jam
土:土 NounTag ; ! soil つち
フラノクマリン:フラノクマリン NounTag ; ! furanocoumarin
食用:食用 NounTag ; ! edible しょくよう
海外:かいがい NounTag ; ! foreign/overseas かいがい
たまご:たまご NounTag ; ! Dir/LR
卵:卵 NounTag ; ! egg
日本語:日本語 NounTag ; ! Japanese language nihongo
北側:北側 NounTag ; ! north side kitagawa
会社:会社 NounTag ; ! company kaisha
学校:学校 NounTag ; ! school gakkou
公式サイト:公式サイト NounTag ; ! official site koushikisaito
絞り:絞り NounTag ; ! しぼり
物品:物品 NounTag ; ! ぶっぴん goods
基本:基本 NounTag ; ! きほん foundation
方法:方法 NounTag ; ! ほうほう method
ドライクリーニング:ドライクリーニング NounTag ; ! dry cleaning
牛乳:牛乳 NounTag ; ! ぎゅうにゅう milk
場合:場合 NounTag ; ! ばあい situation/case
ニュアンス:ニュアンス NounTag ; ! nuance
表現:表現 NounTag ; ! ひょうげん expression
水:水 NounTag ; ! みず water
水気:水気 NounTag ; ! みずけ moisture
選択:洗濯 NounTag ; ! せんたく laundry
字:字 NounTag ; ! じ character
漬物:漬物 NounTag ; ! つけもの pickled vegetables
搾菜:搾菜 NounTag ; ! ザーサイ Szechuan pickles
内部:内部 NounTag ; ! ないぶ interior
外部:外部 NounTag ; ! がいぶ exterior
動作:動作 NounTag ; ! どうさ action
可能:可能 NounTag ; ! かのう potential
流体:流体 NounTag ; ! りゅうたい fluid
タオル:タオル NounTag ; ! towel
同時:同時 NounTag ; ! どうじ simultaneous
中止:中止 NounTag ; ! ちゅうし suspension/interruption
隙間:隙間 NounTag ; ! すきま gap
制限:制限 NounTag ; ! せいげん limit
媒介:媒介 NounTag ; ! ばいかい　intermediary
空中:空中 NounTag ; ! くうちゅう air
マルチスレッド:マルチスレッド NounTag ; ! multi thread
記事:記事 NounTag ; ! きじ article
参考:参考 NounTag ; ! さんこう reference

LEXICON ProperNouns
日本:日本 NPTop ; ! Japan
アメリカ:アメリカ NPTop ; ! America
韓国:韓国 NPTop ; ! South Korea
中国:中国 NPTop ; ! China
イギリス:イギリス NPTop ; ! United Kingdom
ノルウェー:ノルウェー NPTop ; ! Norway
フランス:フランス NPTop ; ! France
スペイン:スペイン NPTop ; ! Spain
イタリア:イタリア NPTop ; ! Italy
ロシア:ロシア NPTop ; ! Russia
フィンランド:フィンランド NPTop ; ! Finland
スウェーデン:スウェーデン NPTop ; ! Sweden
デンマーク:デンマーク NPTop ; ! Denmark
オランダ:オランダ NPTop ; ! Netherland
ドイツ:ドイツ NPTop ; ! Germany
オーストラリア:オーストラリア NPTop ; ! Australia
カナダ:カナダ NPTop ; ! Canada
ベルギー:ベルギー NPTop ; ! Belgium
インド:インド NPTop ; ! India
ギリシャ:ギリシャ NPTop ; ! Greece
シンガポール:シンガポール NPTop ; ! Singapore
インドネシア:インドネシア NPTop ; ! Indonesia
タイ:タイ NPTop ; ! Thailand

LEXICON AdjRoot
静か:静か naAdjTag ; ! しずか　quiet
多い:多い iAdj ; ! おおい　many
様々:様々 naAdjTag ; ! さまざま various
必要:必要 naAdjTag ; ! ひつよう necessary
相対的:相対的 naAdjTag ; ! そうたいてき relative
最終的:最終的 naAdjTag ; ! さいしゅうてき final

LEXICON VerbRoot
食べる:食べる RuVerbInflCP ; ! eat
落とす:落とす UVerbInflCP ; ! drop
含む:含む UVerbInflCP ; ! include
向ける:向け UVerbInflCP ; ! turn
残る:残る UVerbInflCP ; ! remain
呼ぶ:呼ぶ UVerbInflCP ; ! call (something a name/yell out) よぶ
使う:使う UVerbInflCP ; ! つかう use
持つ:持つ UVerbInflCP ; ! もつ carry
行う:行う UVerbInflCP ; ! おこなう occur
死ぬ:死ぬ UVerbInflCP ; ! die
する%<v%>%<ifml%>%<npst%>:する PostShortForm ; ! do
する%<v%>%<pol%>%<npst%>:します # ;
する%<v%>%<cvb_te%>:して # ;
する%<v%>%<caus%>:させ RuVerbInfl ;
する%<v%>%<pass%>:され RuVerbInfl ;

LEXICON Pronouns
私%<prn%>%<pers%>%<p1%>:私 NounPl ; ! I
わたし%<prn%>%<pers%>%<p1%>:わたし NounPl ; ! I
わたくし%<prn%>%<pers%>%<p1%>:わたくし NounPl ; ! I
俺%<prn%>%<pers%>%<p1%>:俺 NounPl ; ! I
おれ%<prn%>%<pers%>%<p1%>:おれ NounPl ; ! I
僕%<prn%>%<pers%>%<p1%>:僕 NounPl ; ! I
ぼく%<prn%>%<pers%>%<p1%>:ぼく NounPl ; ! I
儂%<prn%>%<pers%>%<p1%>:儂 NounPl ; ! I
わい%<prn%>%<pers%>%<p1%>:わい NounPl ; ! I
わし%<prn%>%<pers%>%<p1%>:わし NounPl ; ! I
ワシ%<prn%>%<pers%>%<p1%>:ワシ NounPl ; ! I
余%<prn%>%<pers%>%<p1%>:余 NounPl ; ! I
妾%<prn%>%<pers%>%<p1%>:妾 NounPl ; ! I
拙者%<prn%>%<pers%>%<p1%>:拙者 NounPl ; ! I
おら%<prn%>%<pers%>%<p1%>:おら NounPl ; ! I
おいどん%<prn%>%<pers%>%<p1%>:おいどん NounPl ; ! I
あたし%<prn%>%<pers%>%<p1%>:あたし NounPl ; ! I
自分%<prn%>%<pers%>%<p1%>:自分 NounPl ; ! I
あたい%<prn%>%<pers%>%<p1%>:あたい NounPl ; ! I
あーし%<prn%>%<pers%>%<p1%>:あーし NounPl ; ! I
わい%<prn%>%<pers%>%<p1%>:わい NounPl ; ! I
当方%<prn%>%<pers%>%<p1%>:当方 NounPl ; ! I
小生%<prn%>%<pers%>%<p1%>:小生 NounPl ; ! I
我輩%<prn%>%<pers%>%<p1%>:我輩 NounPl ; ! I
朕%<prn%>%<pers%>%<p1%>:朕 NounPl ; ! I
某%<prn%>%<pers%>%<p1%>:某 NounPl ; ! I
おまえ%<prn%>%<pers%>%<p2%>:おまえ NounPl ; ! You
あなた%<prn%>%<pers%>%<p2%>:あなた NounPl ; ! You
きみ%<prn%>%<pers%>%<p2%>:きみ NounPl ; ! You
君%<prn%>%<pers%>%<p2%>:君 NounPl ; ! You
そなた%<prn%>%<pers%>%<p2%>:そなた NounPl ; ! You
其方%<prn%>%<pers%>%<p2%>:其方 NounPl ; ! You
そちら%<prn%>%<pers%>%<p2%>:そちら NounPl ; ! You
お宅%<prn%>%<pers%>%<p2%>:お宅 NounPl ; ! You
あんた%<prn%>%<pers%>%<p2%>:あんた NounPl ; ! You
貴君%<prn%>%<pers%>%<p2%>:貴君 NounPl ; ! You
卿%<prn%>%<pers%>%<p2%>:卿 NounPl ; ! You
きさま%<prn%>%<pers%>%<p2%>:きさま NounPl ; ! You
貴様%<prn%>%<pers%>%<p2%>:貴様 NounPl ; ! You
お主%<prn%>%<pers%>%<p2%>:お主 NounPl ; ! You
彼%<prn%>%<pers%>%<p3%>:彼 NounPl ; ! He
彼女%<prn%>%<pers%>%<p3%>:彼女 NounPl ; ! She

LEXICON Determiners
この%<det%>%<dem%>:この # ; ! this
その%<det%>%<dem%>:その # ; ! that
あの%<det%>%<dem%>:あの # ; ! that thing over there

LEXICON Conjunctions
しかし%<cnjcoo%>:しかし # ; ! however
また%<cnjcoo%>:また # ; ! also
そして%<cnjcoo%>:そして # ; ! and
だが%<cnjcoo%>:だが # ; ! however
なぜならし%<cnjcoo%>:なぜなら # ; ! because
けれども%<cnjcoo%>:けれども # ; ! although
もしも%<cnjcoo%>:もしも # ; ! if
または%<cnjcoo%>:または # ; ! or

LEXICON Adverbs
特に%<adv%>:特に # ; ! とくに especially
もう%<adv%>:もう # ; ! already
既に%<adv%>:既に # ; ! already
更に%<adv%>:更に # ; ! additionally
とても%<adv%>:とても # ; ! very
だんだん%<adv%>:だんだん # ; ! gradually
徐々に%<adv%>:徐々に # ; ! gradually
とても%<adv%>:とても # ; ! very
ゆっくり%<adv%>:ゆっくり # ; ! slowly


!LEXICON Interjections

LEXICON Digits
DIGITLEX ;

LEXICON Punctuation
。%<sent%>:。 # ;
、%<cm%>:、 # ;
「%<lquot%>:「 # ;
」%<rquot%>:」 # ;
（%<lpar%>:（ # ;
）%<rpar%>:） # ;


! Resources:
! https://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Lexicon
! https://wiki.apertium.org/wiki/Lttoolbox_and_lexc
! https://wiki.apertium.org/wiki/Apertium-specific_conventions_for_lexc
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! http://hfst.sourceforge.net/
! http://www.cis.upenn.edu/~cis639/docs/lexc.html
! https://wiki.apertium.org/wiki/Hfst
